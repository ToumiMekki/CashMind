apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

react {
    root = file("../../")
    reactNativeDir = file("../../node_modules/react-native")
    codegenDir = file("../../node_modules/react-native-codegen")
    cliFile = file("../../node_modules/react-native/cli.js")
}

def enableProguardInReleaseBuilds = false
def jscFlavor = 'org.webkit:android-jsc:+'

// Force androidx.core compatible with compileSdk 34 / AGP 8.2 (avoid 1.16.0 requiring SDK 35)
configurations.all {
    resolutionStrategy {
        force 'androidx.core:core:1.12.0'
        force 'androidx.core:core-ktx:1.12.0'
    }
}

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion
    namespace "com.cashmind"

    defaultConfig {
        applicationId "com.cashmind"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", "false"
        buildConfigField "boolean", "IS_HERMES_ENABLED", (findProperty("hermesEnabled") ?: "true")
    }
    buildFeatures {
        buildConfig true
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")
    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)

// Workaround for "Failed to parse APK" on some devices: install with --no-streaming (full push before parse)
tasks.register("installDebugNoStream") {
    dependsOn("assembleDebug")
    doLast {
        def apk = file("${buildDir}/outputs/apk/debug/app-debug.apk")
        if (apk.exists()) {
            exec {
                commandLine "adb", "install", "-r", "-t", "--no-streaming", apk.absolutePath
            }
        } else {
            throw new GradleException("APK not found: ${apk}")
        }
    }
}
